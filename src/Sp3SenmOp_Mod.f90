#include <SP3DEFINES.H>
MODULE SENMOP_MOD
USE SP3DEF
USE TYPEDEF, ONLY : PinXS_Type, AxFLX_TYPE
!INTEGER,PRIVATE,SAVE :: NG
CONTAINS

SUBROUTINE INIT_SP3SENM(FLX,XS,GEOM,NMESH,NG)
IMPLICIT NONE
TYPE(AXFLX_TYPE) :: FLX(NMESH)
TYPE(PINXS_TYPE) :: XS(NMESH)
TYPE(AXGEOM_TYPE) :: GEOM
INTEGER :: NMESH,NG
INTEGER :: I,IG,ICOMP

!NG = GEOM.NG
!NMESH = GEOM.NMESH
DO I = 1,NMESH
 ! ICOMP = GEOM.COMP(I)
  CALL  SET_SIMTRANS(FLX(I),XS(I),GEOM%H(I),NG)
ENDDO

END SUBROUTINE

SUBROUTINE SET_SIMTRANS(SP3FLX,SP3XS,H,NG)
!SET UP SIMILARITY TRANSFORMATION STUFF
USE MAT2x2OP
IMPLICIT NONE
!INPUT ARG
TYPE(AXFLX_TYPE) SP3FLX
TYPE(PinXS_Type) SP3XS
REAL :: H
INTEGER :: NG
!LOCAL VARIABLES
REAL,DIMENSION(4) :: AHAT,D,A,DINV,QT,SINV,G
REAL :: D0,D2 ,SIGT,SIGR,EIGVEC(4),LAMBDA(2)
REAL :: HSQINV4,KSQ1,KSQ2
INTEGER :: IG
!
HSQINV4=4._8/(H**2)
DO IG = 1,NG
  D0 = SP3XS%XSD(IG)*HSQINV4
  D2 = SP3XS%XSD2(IG)*HSQINV4
  SIGR = SP3XS%XSR(IG)
  SIGT = SP3XS%XST(IG)

  !SET UP D MATRIX
  D(1) = D0;          D(3) = 2._8/5._8*D0
  D(2) = 2.0_8*D0;  D(4) = (4._8*D0+3._8*D2)/5._8
  A(1) = SIGR;        A(4) = SIGT
  A(2) = 0._8;      A(3) = 0
  DINV = MATINV(D)
  AHAT = SUBMATOP(DINV,A)
  CALL SET_EIGS(AHAT,LAMBDA,EIGVEC)
  SINV = MATINV(EIGVEC)
  G=MULTI_MATOP(SINV,AHAT,EIGVEC)
  QT = SUBMATOP(SINV,DINV)
  SP3FLX%KSQ(1:2,IG) = LAMBDA
  !Set Limiting value of kaffa
  SP3FLX%KSQ(1,IG) = MIN(SP3FLX%KSQ(1,IG), MaxK * MaxK)
  SP3FLX%KSQ(2,IG) = MIN(SP3FLX%KSQ(2,IG), MaxK * MaxK)
  SP3FLX%S(:,IG) = EIGVEC
  SP3FLX%QT(:,IG) = QT
ENDDO
END SUBROUTINE

FUNCTION UPDT_Q(SP3FLX,SP3XS,KEFF,IG,NG, lTran)
IMPLICIT NONE
TYPE(AXFLX_TYPE) :: SP3FLX
TYPE(PinXS_Type) SP3XS
REAL :: UPDT_Q(0:4,2),Q(0:4,2),KEFF
LOGICAL :: lTran
INTEGER :: IG,NG
!LOCAL VAR
REAL :: SS(0:4),SIGSIN(NG),PHI(0:4,NG)
!REAL,POINTER :: PHI(:,:,:)
REAL :: CHI,FRAC
INTEGER :: IM, IB, IE

PHI = SP3FLX%PHI(:,1,:)
IB = SP3XS%XSS(IG)%IB;IE = SP3XS%XSS(IG)%IE;
SIGSIN(IB:IE) = SP3XS%xss(ig)%FROM(IB:IE)
FRAC = SP3XS%CHI(IG)/KEFF
!CHI =
UPDT_Q = 0.
!SCATTERING SOURCE UPDATE
SS = 0
DO IM = IB, IE
  SS = SS + PHI(:,IM)*SIGSIN(IM)
  CONTINUE
ENDDO
!FISSION SOURCE UPDATE
UPDT_Q(0:4,1) = SS + FRAC*SP3FLX%PSI
IF(lTran) UPDT_Q(0:4,1) = UPDT_Q(0:4,1) + SP3FLX%TranSrc(0:4, ig)
!SECOND MOMENT SOURCE - OTH SOURCE CONTRITURION

!TRANSEVERSE LEAKGE                       
UPDT_Q(0:2,1) = UPDT_Q(0:2,1) - SP3FLX%TLKG(0:2,1,IG) ! ORIGINAL

!IF( SP3FLX%TLKG(0,1,IG) .LT. 0 )THEN
!    UPDT_Q(0:2,1) = UPDT_Q(0:2,1) - SP3FLX%TLKG(0:2,1,IG)
!ELSE
!    UPDT_Q(0,1) = UPDT_Q(0,1) - SP3FLX%TLKG(0,1,IG)
!    IF( UPDT_Q(0,1).LT. 0 )THEN
!        UPDT_Q(0,1)=0._8
!    ELSE
!        UPDT_Q(1:2,1) = UPDT_Q(1:2,1) - SP3FLX%TLKG(1:2,1,IG)
!    ENDIF    
!ENDIF
!
!IF( UPDT_Q(0,1) .LT. 0 )THEN
!    WRITE(*,*) 'negative Axial src'
!ENDIF

#ifdef TLKG2ND
UPDT_Q(0:2,2) = - 2._8/5._8*SP3FLX%TLKG(0:2,1,IG)! -3._8/5._8*SP3FLX%TLKG(0:2,2,IG)
#else
UPDT_Q(0:2,2) = - 2._8/5._8*SP3FLX%TLKG(0:2,1,IG)
#endif      
!

!IF( UPDT_Q(0,1) .LT. 0 )THEN
!    WRITE(*,*) 'negative Axial src', ig
!    UPDT_Q(0,1)=0._8
!    UPDT_Q(1:2,1)=0._8
!ENDIF
END FUNCTION

FUNCTION UPDT_PSI(PHI,SP3XS,NG)
 IMPLICIT NONE
TYPE(PinXS_Type) SP3XS
REAL :: UPDT_PSI(0:4)
INTEGER :: IG,NG
INTEGER :: J
REAL :: NUFS,PHI(0:4,NG)
UPDT_PSI(0:4) = 0
DO IG = 1,NG
  NUFS = SP3XS%XSNF(IG)
  DO J = 0,4
    UPDT_PSI(J) = UPDT_PSI(J) + NUFS*PHI(J,IG)
  ENDDO
  CONTINUE
ENDDO
CONTINUE
END FUNCTION

FUNCTION UPDT_PSISHAPE(PHI,SP3XS,NG)
 IMPLICIT NONE
TYPE(PinXS_Type) SP3XS
REAL :: UPDT_PSISHAPE(1:4)
INTEGER :: IG,NG
INTEGER :: J
REAL :: NUFS,PHI(0:4,NG)
UPDT_PSISHAPE(1:4) = 0
DO IG = 1,NG
  NUFS = SP3XS%XSNF(IG)
  DO J = 1,4
    UPDT_PSISHAPE(J) = UPDT_PSISHAPE(J) + NUFS*PHI(J,IG)
  ENDDO
  CONTINUE
ENDDO
CONTINUE
END FUNCTION

FUNCTION PARTICULAR_SOL(QH,KSQ)
IMPLICIT NONE
REAL,INTENT(IN) :: QH(0:4,2),KSQ(2)
REAL :: PARTICULAR_SOL(0:4,2),C(0:4,2)

REAL :: K4(2),K6(2)
INTEGER :: I

PARTICULAR_SOL(0:4,2) = 0.; C(0:4,2) = 0.

DO I = 1, 2
  K4(I) = KSQ(I)*KSQ(I)
  K6(I) = K4(I)*KSQ(I)
ENDDO

DO I = 1, 2
  C(0,I) = (QH(0,I)*K4(I)+3._8*QH(2,I)*KSQ(I)+5._8*QH(4,I)*(21._8+2._8*KSQ(I)))/K6(I)
  C(1,I) = (15._8*QH(3,I)+QH(1,I)*KSQ(I))/K4(I)
  C(2,I) = (KSQ(I)*QH(2,I)+35._8*QH(4,I))/K4(I)
  C(3,I) = QH(3,I)/KSQ(I)
  C(4,I) = QH(4,I)/KSQ(I)
ENDDO

PARTICULAR_SOL= C

END FUNCTION

FUNCTION COSHCOEFF(KMAT,SMAT,SHK,PHIAVG,C)
USE MAT2x2OP
IMPLICIT NONE
REAL :: KMAT(4),SMAT(4),SHK(4),PHIAVG(2),C(0:4,2)
REAL :: SINV(4),TPMAT(4),TPVEC(2),COSHCOEFF(2),C0(2)

SINV = MATINV(SMAT);
C0 = C(0,1:2)
TPVEC = SUBMATVECOP(SINV,PHIAVG)-C0
TPMAT = SUBMATOP(MATINV(SHK),KMAT)
COSHCOEFF = SUBMATVECOP(TPMAT,TPVEC)
END FUNCTION

FUNCTION SINHCOEFF(KM,SM,MM,SHK,CHK,A,C)
USE MAT2x2OP
IMPLICIT NONE
INTEGER, PARAMETER :: L=1,R=2
REAL,DIMENSION(4,2) :: KM,SM,MM,SHK,CHK
REAL :: A(2,2),C(0:4,2,2),SINHCOEFF(4),X(4)
REAL :: M4(16),TPMAT(4),TPVEC(4),TPVEC2(2),GPHI(2),GJ(2)
REAL :: MSK(4,2)
INTEGER :: I
!
M4=0
SINHCOEFF=0
TPVEC=0
!4X4 MATRIX SETUP
DO I=1,2
  MSK(:,I)=0
  MSK(:,I) = MULTI_MATOP(MM(:,I), SM(:,I), KM(:,I))
ENDDO
CONTINUE
TPMAT = SUBMATOP(SM(:,L),SHK(:,L))
!M4(1:2,1:2) = TPMAT
M4(1:2) = TPMAT(1:2); M4(5:6) = TPMAT(3:4)
TPMAT = SUBMATOP(SM(:,R),SHK(:,R))
!M4(1:2,3:4) = TPMAT
M4(3:4) = TPMAT(1:2); M4(7:8) = TPMAT(3:4)
TPMAT = MULTI_MATOP(MSK(:,L), CHK(:,L))
!M4(3:4,1:2) = TPMAT
M4(9:10) = TPMAT(1:2); M4(13:14) = TPMAT(3:4)
TPMAT = MULTI_MATOP(MSK(:,R), CHK(:,R))
!M4(3:4,3:4) = -TPMAT
M4(11:12) = -TPMAT(1:2); M4(15:16) = -TPMAT(3:4)

!RHS VECTOR SETUP
GPHI = SUBMATVECOP(SUBMATOP(SM(:,R),CHK(:,R)),A(:,R))
GPHI = GPHI - SUBMATVECOP(SUBMATOP(SM(:,L),CHK(:,L)),A(:,L))
TPVEC2 = C(0,:,R) - C(1,:,R) + C(2,:,R) - C(3,:,R) + C(4,:,R)
GPHI = GPHI + SUBMATVECOP(SM(:,R),TPVEC2)
TPVEC2 = C(0,:,L) + C(1,:,L) + C(2,:,L) + C(3,:,L) + C(4,:,L)
GPHI = GPHI - SUBMATVECOP(SM(:,L),TPVEC2)
!GPHI = GPHI - TPVEC2
!
GJ = 0;
TPVEC2= SUBMATVECOP(SUBMATOP(MSK(:,L),SHK(:,L)),A(:,L))
GJ = -TPVEC2
TPVEC2=  SUBMATVECOP(SUBMATOP(MSK(:,R),SHK(:,R)),A(:,R))
GJ = GJ -TPVEC2
TPVEC2 = C(1,:,L)+3._8*C(2,:,L)+6._8*C(3,:,L)+10._8*C(4,:,L)
GJ = GJ - SUBMATVECOP(SUBMATOP(MM(:,L),SM(:,L)),TPVEC2)
!
TPVEC2 = C(1,:,R)-3._8*C(2,:,R)+6._8*C(3,:,R)-10._8*C(4,:,R)
GJ = GJ + SUBMATVECOP(SUBMATOP(MM(:,R),SM(:,R)),TPVEC2)
TPVEC(1:2) = GPHI
TPVEC(3:4) = GJ
CALL DIR4X4(M4,TPVEC,X)
SINHCOEFF = X
END FUNCTION

FUNCTION SINHCOEFF1N(KM,SM,MM,SHK,CHK,A,C,IDX)
USE MAT2x2OP
IMPLICIT NONE
REAL,DIMENSION(4) :: KM,SM,MM,SHK,CHK,LHS
REAL :: A(2),C(0:4,2),SINHCOEFF1N(2),TP(4),RHS(2),MPHIS(2),MJS(2)
REAL :: ABD(4),LP_S(0:4,2),DLP_S(0:4,2),PPHIS(2),PJS(2)
INTEGER :: IDX,I

!DATA ABD(:,:) / 0.5_8,  -0.125_8, 0.625_8,  0.625_8/
DATA ABD(:) / 0.5_8,   0.625_8, -0.125_8, 0.625_8/
!LEGENDRE POLYNOMIAL AT X= -1
DATA LP_S(:,1) / 1._8, -1._8,  1._8,  -1._8,  1._8/
!LEGENDRE POLYNOMIAL AT X= 1
DATA LP_S(:,2) / 1._8,  1._8,  1._8,   1._8,  1._8/
!DERIVATIVES OF LEGENDRE POLYNOMIAL AT X=-1
DATA DLP_S(:,1) / 0._8,  1._8, -3._8,   6._8, -10._8/
!DERIVATIVES OF LEGENDRE POLYNOMIAL AT X=1
DATA DLP_S(:,2) / 0._8,  1._8,  3._8,   6._8,  10._8/

SINHCOEFF1N = 0.

LHS = MULTI_MATOP(ABD(:),SM,SHK) + MULTI_MATOP(MM,SM,KM,CHK)
!CONTRIBUTION OF PARTICULAR SOLTION TO CURRENT AND FLUX TERMS
MPHIS = 0.; MJS = 0.
DO I = 0,4
  MPHIS = MPHIS + LP_S(I,IDX)*C(I,:)
  MJS =  MJS + DLP_S(I,IDX)*C(I,:)
ENDDO
  !MODAL SPACE -> ORIGINAL SPACE
PPHIS = SUBMATVECOP(SM, MPHIS) !PARTICULAR SOLUTION PART OF SURFACE FLUX
PJS = -SUBMATVECOP(SUBMATOP(MM,SM), MJS) !PARTICULAR SOLUTION PART OF SURFACE CURRENT
RHS = PJS + (-1._8)**(IDX+1)*SUBMATVECOP(ABD(:), PPHIS)

TP = MULTI_MATOP(MM, SM, KM, SHK) + MULTI_MATOP(ABD, SM, CHK)
RHS = RHS + (-1._8)**(IDX+1)*SUBMATVECOP(TP,A)
SINHCOEFF1N = SUBMATVECOP(MATINV(LHS), RHS)
END FUNCTION

FUNCTION FLX_4THOD_EXPANSION(A,B,C,KSQ)
IMPLICIT NONE

REAL :: A,B,C(0:4),KSQ
REAL :: FLX4TH(0:4),FLX_4THOD_EXPANSION(0:4)
REAL :: SINHK,COSHK,K,K2,K4,K0

K = SQRT(KSQ)
K2 = KSQ
K4 = KSQ*KSQ
K0 = MIN(K, MaxK)
SINHK = SINH(K0); COSHK = COSH(K0)

FLX4TH = 0.
FLX4TH(0) = C(0)+SINHK*A/K
FLX4TH(1) = C(1)+3._8/K*(COSHK - SINHK/K)*B
FLX4TH(2) = C(2)-5._8*(3._8*COSHK/K2-(1+3._8/K2)*SINHK/K)*A
FLX4TH(3) = C(3)+7._8/K*((1._8+15._8/K2)*COSHK-(6._8+15._8/K2)*SINHK/K)*B
FLX4TH(4) = C(4)-9._8*(5._8/K2*(2._8+21._8/K2)*COSHK-(1._8+45._8/K2+105._8/K4)*SINHK/K)*A
FLX_4THOD_EXPANSION = FLX4TH
END FUNCTION

FUNCTION SINHMAT(K1,K2,X)
IMPLICIT NONE
REAL :: X,K1,K2,SINHMAT(4)
SINHMAT= 0
SINHMAT(1) = SINH(K1*X)
SINHMAT(4) = SINH(K2*X)
END FUNCTION

FUNCTION COSHMAT(K1,K2,X)
IMPLICIT NONE
REAL :: X,K1,K2,COSHMAT(4)
COSHMAT= 0
COSHMAT(1) = COSH(K1*X)
COSHMAT(4) = COSH(K2*X)
END FUNCTION

FUNCTION KMAT(K1,K2)
IMPLICIT NONE
REAL :: K1,K2,KMAT(4)
KMAT = 0
KMAT(1) = K1; KMAT(4) = K2
END FUNCTION

FUNCTION MMAT(D0,D2,H)
IMPLICIT NONE
REAL :: D0,D2,H,MMAT(4)
REAL :: HINV
MMAT = 0; HINV = 2._8/H
MMAT(1) = HINV*D0;   MMAT(2) = 2._8*HINV*D0
MMAT(4) = HINV*D2
END FUNCTION

!      FUNCTION INTERFACE_J(A,B,C,KM,SM,MM,I)
!      IMPLICIT NONE
!      REAL :: INTERFACE_J(2)
!      REAL :: A(2),B(2),SM(2,2),KM(2,2),MM(22),K(2)
!      INTEGER :: I
!
!      END FUNCTION
FUNCTION CAL_J(A,B,C,KM,SM,MM,X)
USE MAT2x2OP
IMPLICIT NONE
REAL :: INTERFACE_J(2)
REAL :: A(2),B(2),SM(4),KM(4),MM(4),K(2),C(0:4,2)
REAL :: X, CAL_J(2),MJ(2),MJH(2),MJP(2),SHK(4),CHK(4)
REAL :: MSK(4),T
INTEGER :: I
K(1) = KM(1); K(2) = KM(4)
MSK = MULTI_MATOP(MM,SM,KM)
MJP=0
DO I = 0, 4
  MJP = MJP + DLP(I,X)*C(I,:)
  T=DLP(I,X)
  CONTINUE
ENDDO

SHK = SINHMAT(K(1),K(2),X)
CHK = COSHMAT(K(1),K(2),X)
MJH = SUBMATVECOP(SHK,A) + SUBMATVECOP(CHK,B)
CAL_J = -SUBMATVECOP(MSK,(MJH)) - SUBMATVECOP(SUBMATOP(MM,SM),MJP)
END FUNCTION

FUNCTION CAL_CompJ(A,B,C,KM,SM,MM,X)
USE MAT2x2OP
IMPLICIT NONE
REAL :: INTERFACE_J(2), CAL_CompJ(2,2)
REAL :: A(2),B(2),SM(4),KM(4),MM(4),K(2),C(0:4,2)
REAL :: X, CAL_J(2),MJ(2),MJH(2),MJP(2),SHK(4),CHK(4)
REAL :: MSK(4),T, SK(4), DelJ(2)
INTEGER :: I
Cal_CompJ = 0
K(1) = KM(1); K(2) = KM(4)
MSK = MULTI_MATOP(MM,SM,KM)
SK = MULTI_MATOP(SM, KM)
MJP=0
DO I = 0, 4
  MJP = MJP + DLP(I,X)*C(I,:)
  T=DLP(I,X)
  CONTINUE
ENDDO

SHK = SINHMAT(K(1),K(2),X)
CHK = COSHMAT(K(1),K(2),X)
MJH = SUBMATVECOP(SHK,A) + SUBMATVECOP(CHK,B)
DelJ = SUBMATVECOP(SK, MJH) + SUBMATVECOP(SM, MJP)
CAL_J = SUBMATVECOP(MM, DelJ)
Cal_CompJ(1, 1) = -MM(1) * DelJ(1); Cal_CompJ(2, 1) = -MM(2) * DelJ(2)
Cal_CompJ(2, 2) = -MM(4) * DelJ(2)
!CAL_J = -SUBMATVECOP(MSK,(MJH)) - SUBMATVECOP(SUBMATOP(MM,SM),MJP)
END FUNCTION

FUNCTION CAL_SP3FLX(A,B,C,KM,SM,X)
USE MAT2x2OP
IMPLICIT NONE
REAL :: A(2),B(2),SM(4),KM(4),K(2),X,CAL_SP3FLX(2)
REAL :: C(0:4,2),SHK(4),CHK(4),MPHI(2)
REAL :: LPV
INTEGER :: I
K(1) = KM(1); K(2) = KM(4)
SHK = SINHMAT(K(1),K(2),X)
CHK = COSHMAT(K(1),K(2),X)
MPHI = SUBMATVECOP(CHK,A)+SUBMATVECOP(SHK,B)
DO I=0,4
  LPV = LP(I,X)
  MPHI = MPHI + LPV*C(I,:)
ENDDO
CAL_SP3FLX = SUBMATVECOP(SM,MPHI)
END FUNCTION

FUNCTION Cal_CmfdRelation(PHIL, PHIR, PHIS, Beta1, Beta2, Jnet)
IMPLICIT NONE
REAL :: PHIL, PHIR, PHIS, Beta1, Beta2, Jnet
REAL :: Cal_CmfdRelation(2)
REAL :: BETAL, BETAR
REAL :: DHATL0, DHATR0, JFDML, JFDMR, DENO
REAL :: Dtil, DHAT
!Suface relation
BETAL = BETA1;
BETAR = BETA2;
JFDML=-2._8*betal*(PHIS-PHIL); DHATL0=(JFDML-JNET)/(2._8*PHIS+2._8*PHIL)
JFDMR=-betar*2._8*(PHIR-PHIS); DHATR0 = (JFDMR-JNET)/(2._8*PHIR+2._8*PHIS)
IF(ABS(JNET) .LT. 1.0E-5) THEN
  DHATL0 =0; DHATR0=0
ENDIF

DENO = betar+betal-DHATR0+DHATL0
DTIL= 2._8*(betar*betal+DHATR0*DHATL0)/deno
DHAT = 2._8*(betal*DHATR0+betar*DHATL0)/deno
IF(ABS(DTIL) .GT. 1.0E+3 .OR. ABS(DHAT) .GT. 1.0E+3 )  THEN
  !print *, DTIL, DHAT
  BETAL = 0.95 * JNET / (2._8*(PHIS-PHIL)); BETAR = 0.95 * JNET /( 2._8*(PHIR-PHIS))
  JFDML=-2._8*betal*(PHIS-PHIL); DHATL0=(JFDML-JNET)/(2._8*PHIS+2._8*PHIL)
  JFDMR=-betar*2._8*(PHIR-PHIS); DHATR0 = (JFDMR-JNET)/(2._8*PHIR+2._8*PHIS)
  DENO = betar+betal-DHATR0+DHATL0
  DTIL= 2._8*(betar*betal+DHATR0*DHATL0)/deno
  DHAT = 2._8*(betal*DHATR0+betar*DHATL0)/deno
  !print *, DTIL, DHAT
ENDIF
Cal_CmfdRelation = (/DTIL, DHAT/)
END FUNCTION

FUNCTION LP(I,X)
IMPLICIT NONE
INTEGER :: I,J
REAL :: X,Y,LP,LPCOEFF(0:4,0:4)
DATA LPCOEFF    /    1._8,    0._8,    0._8,   0._8,    0._8, &
                     0._8,    1._8,    0._8,   0._8,    0._8, &
                   -0.5_8,    0._8,   1.5_8,   0._8,    0._8, &
                     0._8,  -1.5_8,    0._8,  2.5_8,    0._8, &
                  0.375_8,    0._8, -3.75_8,   0._8, 4.375_8 /
Y=0
DO J=0,I
  Y=Y+(X**J)*LPCOEFF(J,I)
ENDDO
LP=Y
END FUNCTION

FUNCTION DLP(I,X)
IMPLICIT NONE
INTEGER :: I,J
REAL :: X,Y,DLP,LPCOEFF(0:4,0:4)
DATA LPCOEFF    /    0._8,    0._8,    0._8,   0._8,    0._8, &
                     1._8,    0._8,    0._8,   0._8,    0._8, &
                     0._8,    3._8,    0._8,   0._8,    0._8, &
                   -1.5_8,    0._8,   7.5_8,   0._8,    0._8, &
                     0._8,  -7.5_8,    0._8, 17.5_8,    0._8 /
Y=0
DO J=0,I
  Y=Y+(X**J)*LPCOEFF(J,I)
ENDDO
DLP = Y
END FUNCTION
END MODULE
